import "Sock";

##	Container for the `Socket` object
struct Socket class {
	##	Contains a pointer to `Socket.class`
	##	@field	Pointer
	var socket;
	##	Value of the Socket's address
	##	@field	String
	var address;
	##	Value of the Socket's port
	##	@field	Number
	var port;

	##	Constructor for `Socket`
	##	@param	address	String,Pointer	Value of a given address, or (by means of a `Server` object) a predefined pointer to `Socket.class`
	##	@param	port	Number	Value of a given port, unused in the case of `address` being a referrent to Socket.class
	init(address, port) => {
		port == null then {
			this.socket = address;
		} else {
			this.socket = ("Sock.OPEN" _OUTER_CALL_ {address, port});
			this.address = address;
			this.port = port;
		};
	};

	##	Closes a `Socket`
	close() => {
		"Sock.CLOSE" _OUTER_CALL_ (this.socket);
		this.socket = null;
	};

	##	Gets a `Socket.Output` object referring to the current instance of `Socket`
	##	@return	Socket.Output	The relevant `Output` object
	getOutput() => {
		return new Output(this.socket);
	};

	##	Gets a `Socket.Input` object referring to the current instance of `Socket`
	##	@return	Socket.Input	The relevant `Input` object
	getInput() => {
		return new Input(this.socket);
	};

	##	Gets the value of the Socket's connecting address
	##	@return	String	The relevant address
	getAddress() => {
		return this.address;
	};

	##	Gets the value of the Socket's connecting port
	##	@return	Number	The relevant port
	getPort() => {
		return this.port;
	};

	##	Returns a `String` representation of the object
	##	@return	String	Representation of the object
	getStr() => {
		return address + ":" + str port
	};

	##	Container for the Output stream of a `Socket` object
	struct Output class {
		##	Contains a pointer to `DataOutputStream.class`
		##	@field	Pointer
		var out;

		##	Constructor for `Socket.Output`
		##	@param	socket	Pointer	Value referring to a `Socket.class` instance
		init(socket) => {
			this.out = ("Sock.OUT.OPEN" _OUTER_CALL_ socket);
		};

		##	Sends data through the output stream
		##	@param	string	String	Value to be sent
		send(string) => {
			"Sock.OUT.SEND" _OUTER_CALL_ {this.out, string};
		};

		##	Closes a `Socket.Output` object
		close() => {
			"Sock.OUT.CLOSE" _OUTER_CALL_ (this.out);
			this.out = null;
		};
	};

	##	Container for the Input stream of a `Socket` object
	struct Input class {
		##	Contains a pointer to `DataInputStream.class`
		##	@field	Pointer
		var input;

		##	Constructor for `Socket.Input`
		##	@param	socket	Pointer	Value referring to a `Socket.class` instance
		init(socket) => {
			this.input = ("Sock.IN.OPEN" _OUTER_CALL_ socket);
		};

		##	Reads in any incoming values to the Socket, pauses the thread until a value is resolved
		##	@return	String	Value of data read
		read() => {
			return ("Sock.IN.READ" _OUTER_CALL_ (this.input));
		};

		##	Closes a `Socket.Input` object
		close() => {
			"Sock.IN.CLOSE" _OUTER_CALL_ (this.input);
			this.input = null;
		};
	};
};

##	Container for the `Server` object
struct Server class {
	##	Contains a pointer to `ServerSocket.class`
	##	@field	Pointer
	var server;
	##	Value of the Server's port
	##	@field	Number
	var port;

	##	Constructor for `Server`
	##	@param	port	Number	Value of the port on which the Server opens
	init(port) => {
		this.server = ("Sock.SERVER.OPEN" _OUTER_CALL_ port);
		this.port = port;
	};

	##	Closes a `Server` object
	close() => {
		"Sock.SERVER.CLOSE" _OUTER_CALL_ (this.server);
		this.server = null;
	};

	##	Waits for an incoming socket to connect to the server, pauses the thread until a socket connects
	##	@return	Socket	Object of the connected socket
	accept() => {
		return new Socket("Sock.SERVER.ACCEPT" _OUTER_CALL_ (this.server));
	};

	##	Gets the value of the Server's connecting port
	##	@return	Number	The relevant port
	getPort() => {
		return this.port;
	};

	##	Returns a `String` representation of the object
	##	@return	String	Representation of the object
	getStr() => {
		return "LocalServer:" + str port;
	};
};