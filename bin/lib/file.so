# Benjamin Park
# 29-06-2020

import "LIB_FileIO";

struct File class {
	var pointer;

	init(final filename) => {
		this.pointer = ("LIB_FileIO.INIT" _OUTER_CALL_ filename);
	}

	exists() => {
		return "LIB_FileIO.EXISTS" _OUTER_CALL_ (this.pointer);
	}

	create() => {
		return "LIB_FileIO.CREATE" _OUTER_CALL_ (this.pointer);
	}

	getStr() => {
		return str this.pointer;
	}

	getReader() => {
		return new Reader(this.pointer);
	}

	getWriter() => {
		return new Writer(this.pointer);
	}

	struct Reader class {
		var pointer;

		init(ref pointer) => {
			this.pointer = ("LIB_FileIO.READER.INIT" _OUTER_CALL_ pointer);
		}

		readLine() => {
			var line = ("LIB_FileIO.READER.GETLINE" _OUTER_CALL_ pointer);
			line == null then {
				close();
			}
			return line;
		}

		read(i) => {
			pointer == null then {
				return null;
			}
			i == null then {
				i = 1;
			}
			var s = "";
			j in {0 until i} do {
				var c = ("LIB_FileIO.READER.GET" _OUTER_CALL_ pointer);
				c == null then {
					s == "" then {
						s = null;
					}
					close();
					break;
				} else {
					s += c;
				}
			}
			return s;
		}

		close() => {
			"LIB_FileIO.READER.CLOSE" _OUTER_CALL_ pointer;
			pointer = null;
		}

		getStr() => {
			return str this.pointer;
		}
	}

	struct Writer class {
		var pointer;

		init(ref pointer) => {
			this.pointer = ("LIB_FileIO.WRITER.INIT" _OUTER_CALL_ pointer);
		}

		write(ref string) => {
			"LIB_FileIO.WRITER.WRITE" _OUTER_CALL_ (pointer, string);
		}

		close() => {
			"LIB_FileIO.WRITER.CLOSE" _OUTER_CALL_ pointer;
			pointer = null;
		}

		getStr() => {
			return str this.pointer;
		}
	}
}