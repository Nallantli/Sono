load "system";
load "vector";
load "math";

String::pop(ref this) => {
	this = (vec this).pop().join();
	return this;
};

String::shift(ref this) => {
	this = (vec this).shift().join();
	return this;
};

String::front(ref this) => {
	return (vec this).front();
};

String::back(ref this) => {
	return (vec this).back();
};

String::isEmpty(ref this) => {
	return (len this == 0);
};

String::at(ref this, ref index) => {
	return (vec this)[index];
};

String::push(ref this, ref value) => {
	this += str value;
	return this;
};

String::contains(ref this, ref pattern) => {
	return (len this.match(pattern) > 0);
};

String::dist(ref this, ref string, ref costDel, ref costIns, ref costRep) => {
	return (vec this).dist(string, costDel, costIns, costRep);
};

String::nGram(ref this, ref n, delim) => {
	delim == null then {
		delim = "\\s";
	};
	var s = this.split(delim);
	return s.nGram(n);
};

String::split(ref this, ref pattern) => {
	var stringArray = vec this;
	var coords = this.match(pattern);
	var array = {};
	var mode = true;
	var c = 0;
	var curr = {};
	i in {0 until len this} do {
		c < len coords then {
			coords[c][1] == i then {
				mode = true;
				c = c + 1;
			};
			c < len coords then {
				coords[c][0] == i then {
					array = array + { curr.join() };
					curr = {};
					mode = false;
				};
			};
		};
		mode then {
			curr = curr + { stringArray[i] };
		};
	};
	curr != {} then {
		array = array + { curr.join() };
	};
	return array;
};

String::replace(ref this, ref pattern, ref value) => {
	var stringArray = vec this;
	var coords = this.match(pattern);
	var array = {};
	var mode = true;
	var c = 0;
	var curr = {};
	i in {0 until len this} do {
		c < len coords then {
			coords[c][1] == i then {
				mode = true;
				c = c + 1;
			};
			c < len coords then {
				coords[c][0] == i then {
					array = array + { curr.join(), value };
					curr = {};
					mode = false;
				};
			};
		};
		mode then {
			curr = curr + { stringArray[i] };
		};
	};
	curr != {} then {
		array = array + { curr.join() };
	};
	this = array.join();
	return this;
};