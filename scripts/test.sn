load "console";

let transformation = {
	# All consonants that are followed by another consonant (except nasals) are [+long]; /kk/ -> /kː/
	Af : [-syl, -nasal] -> [+long] // null ~ [-syl, -approx],
	# All vowels followed by another vowel of equal frontness are [+long]; /aa/ -> /aː/, /ei/ -> /eː/
	Af : [+syl, A-back, A-front] -> [+long] // null ~ [+syl, A-back, A-front],
	# Consonants are palatalized before /i(ː)/
	S : [-syl] -> [+DOR, +high, -low, +front, -back] // null ~ [+syl, +high, +front],
	# Consonants are palatalized before /j/ and the approximant is removed
	Af : [-syl] -> [+DOR, +high, -low, +front, -back] // null ~ [-syl, +approx, +high, +front],
	# Palatalized coronals are further palatalized into fricatives/affricates; /tʲ/ -> /t_ɕ/
	S : [+cons, -syl, -son, +COR, +high, -low, +front, -back] -> [+del, +str, +dist, -front, +back] // null ~ [+syl],
	# Miscellaneous palatalization of consonants that are unable to be grouped with coronals
	S : 'hʲ' -> 'ç' // null ~ [+syl],
	S : 'nʲ' -> 'ɲ' // null ~ [+syl],
	# Anterior consonants are palatalized before /ɯ(ː)/
	S : [-syl, -approx, +ant, -dist] -> [+DOR, +high, -low, +front, -back] // null ~ [+syl, +high, +back],
	# /ɯ(ː)/ is centralized to /ɨ(ː)/ after a palatalized consonant
	S : [+syl, +high, +back, A-long] -> [+syl, +high, -back, A-long] // [-syl, +high, +front] ~ null,
	# Palatalized anterior consonants become sibilants; /tʲ/ -> /t_s/
	S : [-syl, -approx, +ant, -dist] -> [+del, +str, -DOR] // null ~ [+syl, +high, -back, -front],
	# Vowels before the moraic nasal are nasalized
	S : [+syl] -> [+nasal] // null ~ 'ɴ',
	# The moraic nasal assimilates to the place of the following consonant
	S : 'ɴ' -> [C-LAB, C-round, C-ld, C-COR, C-ant, C-dist, C-DOR, C-high, C-low, C-front, C-back] // null ~ [-syl, +cons, C-LAB, C-round, C-ld, C-COR, C-ant, C-dist, C-DOR, C-high, C-low, C-front, C-back]
};

let s = word "koɴpjɯɯtaa";

let result = str (s >> transformation);

print(result);