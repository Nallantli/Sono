load "system";

var transformation = {
	# All consonants that are followed by another consonant (except nasals) are [+long]; /kk/ -> /kː/
	Af : [-syl, -nasal] -> {[+long]} // {} ~ {[-syl, -approx]},
	# All vowels followed by another vowel of equal frontness are [+long]; /aa/ -> /aː/, /ei/ -> /eː/
	Af : [+syl, A-back, A-front] -> {[+long]} // {} ~ {[+syl, A-back, A-front]},
	# Consonants are palatalized before /i(ː)/
	S : [-syl] -> {[+DOR, +high, -low, +front, -back]} // {} ~ {[+syl, +high, +front]},
	# Consonants are palatalized before /j/ and the approximant is removed
	Af : [-syl] -> {[+DOR, +high, -low, +front, -back]} // {} ~ {[-syl, +approx, +high, +front]},
	# Palatalized coronals are further palatalized into fricatives/affricates; /tʲ/ -> /t_ɕ/
	S : [+cons, -syl, -son, +COR, +high, -low, +front, -back] -> {[+del, +str, +dist, -front, +back]} // {} ~ {[+syl]},
	# Miscellaneous palatalization of consonants that are unable to be grouped with coronals
	S : 'hʲ' -> {'ç'} // {} ~ {[+syl]},
	S : 'nʲ' -> {'ɲ'} // {} ~ {[+syl]},
	# Anterior consonants are palatalized before /ɯ(ː)/
	S : [-syl, -approx, +ant, -dist] -> {[+DOR, +high, -low, +front, -back]} // {} ~ {[+syl, +high, +back]},
	# /ɯ(ː)/ is centralized to /ɨ(ː)/ after a palatalized consonant
	S : [+syl, +high, +back, A-long] -> {[+syl, +high, -back, A-long]} // {[-syl, +high, +front]} ~ {},
	# Palatalized anterior consonants become sibilants; /tʲ/ -> /t_s/
	S : [-syl, -approx, +ant, -dist] -> {[+del, +str, -DOR]} // {} ~ {[+syl, +high, -back, -front]},
	# Vowels before the moraic nasal are nasalized
	S : [+syl] -> {[+nasal]} // {} ~ {'ɴ'},
	# The moraic nasal assimilates to the place of the following consonant
	S : 'ɴ' -> {[C-LAB, C-round, C-ld, C-COR, C-ant, C-dist, C-DOR, C-high, C-low, C-front, C-back]} // {} ~ {[-syl, +cons, C-LAB, C-round, C-ld, C-COR, C-ant, C-dist, C-DOR, C-high, C-low, C-front, C-back]}
};

var s = word "tikkeidɯɴbaɴkjɯ";

t in transformation do {
	print(t);
	print(s = s >> t);
};