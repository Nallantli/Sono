load "system";
load "vector";
load "neural";

# Binary Adder - Neural Network
# Adds two binary digits with a possible carry, formatted as {carry-alpha, a, b} => {c, carry-beta}
# Network size might be overkill for an adder, but it's good for stress testing

var n = new Network.Basic(3, 6, 2, 2);

var data = {
	{{0,0,0}, {0,0}},
	{{0,0,1}, {1,0}},
	{{0,1,0}, {1,0}},
	{{0,1,1}, {0,1}},
	{{1,0,0}, {1,0}},
	{{1,0,1}, {0,1}},
	{{1,1,0}, {0,1}},
	{{1,1,1}, {1,1}}
};

var cycles = 0;
var success = 0;
success <= 0.95 do {
	success = 0;
	var sets = {};
	i in {0 until 10} do {
		sets += data.randomize();
	}
	s in sets do {
		cycles += 1;
		var ret = n.train(0.5, s[0], s[1]).map(round);
		success += ret == s[1];
	}
	success /= len sets;
	println("Success Rate:\t" + str (success * 100).round() + "%");
}

println("Cycles:\t" + str cycles);
data.map((e) => {
	println(str e[0] + " -> " + str n.propogate(e[0]).map((x) => {return x.round(2)}));
});