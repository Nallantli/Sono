load "graphics";
load "system";

var SIZE_X = 60;
var SIZE_Y = 40;

var valueArray = {};
var shapeArray = {};

x in {0 until SIZE_X} do {
	var temp = {};
	y in {0 until SIZE_Y} do {
		temp += { Random.float().round() };
	};
	valueArray += {temp};
};

var window = new Window("Conway's Game of Life", SIZE_X * 10, SIZE_Y * 10, () => {
	println("Kind regards to the late John Conway");
	exit();
});
var graphics = window.getGraphics();

x in {0 until SIZE_X} do {
	var temp = {};
	y in {0 until SIZE_Y} do {
		var shape = new Shape.Rectangle(Color.WHITE, x * 10, y * 10, 10, 10);
		temp += { shape };
		graphics.add(shape);
	};
	shapeArray += { temp };
};

neighborCount(&x, &y) => {
	var c = 0;
	c += valueArray[(x + SIZE_X + 1) % SIZE_X][y] == 1;
	c += valueArray[(x + SIZE_X - 1) % SIZE_X][y] == 1;
	c += valueArray[x][(y + SIZE_Y + 1) % SIZE_Y] == 1;
	c += valueArray[x][(y + SIZE_Y - 1) % SIZE_Y] == 1;
	c += valueArray[(x + SIZE_X + 1) % SIZE_X][(y + SIZE_Y + 1) % SIZE_Y] == 1;
	c += valueArray[(x + SIZE_X + 1) % SIZE_X][(y + SIZE_Y - 1) % SIZE_Y] == 1;
	c += valueArray[(x + SIZE_X - 1) % SIZE_X][(y + SIZE_Y + 1) % SIZE_Y] == 1;
	c += valueArray[(x + SIZE_X - 1) % SIZE_X][(y + SIZE_Y - 1) % SIZE_Y] == 1;
	return c;
};

refresh() => {
	x in {0 until SIZE_X} do {
		y in {0 until SIZE_Y} do {
			valueArray[x][y] == 1 then {
				shapeArray[x][y].setColor(Color.BLACK);
			} else {
				shapeArray[x][y].setColor(Color.WHITE);
			};
		};
	};
	var newValueArray = {};
	x in {0 until SIZE_X} do {
		var temp = {};
		y in {0 until SIZE_Y} do {
			var c = neighborCount(x, y);
			valueArray[x][y] then {
				temp += { c == 2 || c == 3 };
			} else {
				temp += { c == 3 };
			};
		};
		newValueArray += { temp };
	};
	valueArray = newValueArray;
	window.repaint();
};

var start = timeMS();
true do {
	var lapse = timeMS() - start;
	lapse > 30 then {
		start = timeMS();
		refresh();
	};
};