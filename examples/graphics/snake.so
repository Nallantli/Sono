load "graphics";
load "system";

var SIZE_X = 30;
var SIZE_Y = 30;
var SIZE_SQUARE = 15;

var window = new Window("Snake Game", SIZE_X * SIZE_SQUARE, SIZE_Y * SIZE_SQUARE, exit);
var graphics = window.getGraphics();

var valueArray = alloc SIZE_X;
var shapeArray = alloc SIZE_X;
var direction = 0;
var newDir = 0;
var posX = 30;
var posY = 20;
var size = 1;
var LOST = false;

x in {0 until SIZE_X} do {
	valueArray[x] = alloc SIZE_Y;
	y in {0 until SIZE_Y} do {
		valueArray[x][y] = 0;
	}
}

x in {0 until SIZE_X} do {
	shapeArray[x] = alloc SIZE_Y;
	y in {0 until SIZE_Y} do {
		var shape = new Shape.Rectangle(Color.WHITE, null, x * SIZE_SQUARE, y * SIZE_SQUARE, SIZE_SQUARE, SIZE_SQUARE);
		shapeArray[x][y] = shape;
		graphics.add(shape);
	}
}

window.onKeyPress((event) => {
	event.charStr == "W" && direction != 2 then {
		newDir = 3;
	} else event.charStr == "S" && direction != 3 then {
		newDir = 2;
	} else event.charStr == "A" && direction != 0 then {
		newDir = 1;
	} else event.charStr == "D" && direction != 1 then {
		newDir = 0;
	}
});

setApple() => {
	var x = Random.int(0, SIZE_X);
	var y = Random.int(0, SIZE_Y);
	valueArray[x][y] != 0 do {
		x = Random.int(0, SIZE_X);
		y = Random.int(0, SIZE_Y);
	}
	valueArray[x][y] = -1;
}

refresh() => {
	!LOST then {
		direction = newDir;

		direction switch {
			0 goto {
				posX = (posX + 1 + SIZE_X) % SIZE_X;
			}
			1 goto {
				posX = (posX - 1 + SIZE_X) % SIZE_X;
			}
			2 goto {
				posY = (posY + 1 + SIZE_Y) % SIZE_Y;
			}
			3 goto {
				posY = (posY - 1 + SIZE_Y) % SIZE_Y;
			}
		}

		valueArray[posX][posY] == -1 then {
			size += 1;
			setApple();
		} else valueArray[posX][posY] > 0 then {
			LOST = true;
			println("You Lost!");
		}

		valueArray[posX][posY] = size;
	}

	x in {0 until SIZE_X} do {
		y in {0 until SIZE_Y} do {
			valueArray[x][y] > 0 then {
				shapeArray[x][y].setFill(Color.GREEN);
			} else valueArray[x][y] < 0 then {
				shapeArray[x][y].setFill(Color.RED);
			} else {
				shapeArray[x][y].setFill(Color.BLACK);
			}
		}
	}

	x in valueArray do {
		y in x do {
			y > 0 then {
				y -= 1;
			}
		}
	}

	window.repaint();
}

setApple();

var start = timeMS();
true do {
	var lapse = timeMS() - start;
	lapse > 80 then {
		start = timeMS();
		refresh();
	}
}